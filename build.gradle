// -----
// SETUP
// -----

configure(rootProject) {

	task wrapper(type: Wrapper) {
		description = 'Generates gradlew and gradlew.bat scripts'
		gradleVersion = '3.5'
		jarFile = "${project.projectDir}/.infra/gradle/gradle-wrapper.jar"
	}

}

task setupGitRepoOnTravis(type: Exec) {
	commandLine './git-repo-travis-setup.sh'
}

task setupGitRepo(type: Exec) {
	commandLine './git-repo-setup.sh'
	if (project.findProperty('on-travis'))
		dependsOn setupGitRepoOnTravis
}

task setupJekyll(type: Exec) {
	dependsOn setupGitRepo

	workingDir 'site-source'
	commandLine 'bundle install --quiet'.split(' ')
}

task setup {
	dependsOn setupGitRepo, setupJekyll
}

// -----
// CLEAN
// -----

task removeProjectPage(type: Delete) {
	delete fileTree('site-source/_pages') {
		include 'xp.*'
	}
}

task removeDocsPage(type: Delete) {
	delete fileTree('site-source/_pages') {
		include '_docs-xp.adoc'
	}
}

task removeDocs(type: Delete) {
	delete 'site-source/_docs/xp'
}

task removeDocNav() {
	doLast {
		description 'Deletes specific lines in _data/naviation.yml'
		File nav = file('site-source/_data/navigation.yml')
		String fullNav = nav.getText()
		String removedNav = fullNav.replaceAll(/(?m)# @xp:start$\s^docs-xp:$\s(^[\D\d]*$\s)*^# @xp:end/, "# @xp:start\ndocs-xp:\n# @xp:end")
		nav.setText(removedNav)
	}
}

task clean {
	dependsOn removeProjectPage, removeDocsPage, removeDocs, removeDocNav
}

// ------
// CREATE
// ------

task copyProjectPage(type: Copy) {
	from 'junit-pioneer/docs/'
	include 'project-page.*'
	rename 'project-page.(.*)', 'xp.$1'
	into 'site-source/_pages'
}

task copyDocsPage(type: Copy) {
	from 'junit-pioneer/docs/'
	include 'docs-page.*'
	rename 'docs-page.adoc', '_docs-xp.adoc'
	into 'site-source/_pages'
}

task copyDocs(type: Copy) {
	from 'junit-pioneer/docs'
	exclude 'project-page.*', 'docs-page.*', 'docs-nav.yml'
	into 'site-source/_docs/xp'
}

task insertDocNav() {
	doLast {
		description 'Inserts specific lines into _data/naviation.yml'
		String navContent = file('junit-pioneer/docs/docs-nav.yml').getText()
		File navTarget = file('site-source/_data/navigation.yml')
		String emptyNav = navTarget.getText()
		String fullNav = emptyNav.replaceAll(/(?m)# @xp:start$\s^docs-xp:$\s^# @xp:end/, "# @xp:start\ndocs-xp:\n${navContent}# @xp:end")
		navTarget.setText(fullNav)
	}
}

task create {
	dependsOn copyProjectPage, copyDocsPage, copyDocs, insertDocNav
}

// ---------------
// BUILD & PUBLISH
// ---------------

task recreate {
	dependsOn clean, create
}

task buildSite(type: Exec) {
	dependsOn recreate

	workingDir 'site-source'
	commandLine 'bundle exec jekyll build --destination ../site'.split(' ')
}

task pushSite(type: Exec) {
	commandLine './git-commit-push.sh site'.split(' ')
}
